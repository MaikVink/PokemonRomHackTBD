mapscripts Hall1_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_LABORATORYHALL_STATE, 0: Hall1_OnFrame
        VAR_LABORATORYHALL_STATE, 2: Hall1_OnFrame
    ]
}

script Hall1_OnFrame{
    setvar(VAR_LABORATORYHALL_STATE, 1)
}

script Hall1_SignScript_StatueNormal{
    lockall
    msgbox(format("It seems to be of a statue of a pokemon, though the erosion make it hard to determine which pokemon it is supposed to be."))
    releaseall
}

script Hall1_SignScript_PotNormal{
    lockall
    msgbox(format("It just an ordinary pot."))
    releaseall
}

script Hall1_EventScript_GuardLeft{
    lockall
    faceplayer
    msgbox(format("This room leads to rollcall.\n Once you get you retrieve your pokemon you should return here."))
    releaseall
}

script Hall1_EventScript_GuardRight{
    lockall
    faceplayer
    msgbox(format("Today is a special day huh?\n Don't worry initiative!\l We will make sure nothing goes wrong today."))
    releaseall
}

script Hall1_EventScript_Wanderer{
    lockall
    faceplayer
    switch(var(VAR_STORY_PROGRESSION))
    {
        case 0:
        case 1:
        case 2:
        case 3:
        case 4:
            msgbox(format("Have you gotten your pokemon already?\p You can get it from the professor down the hall to the right!"))
        case 5:
            msgbox(format("I heard some rumors that Team A.S. members were trying to infiltrate this place.\p
            Would that be the reason {COLOR LIGHT_GRAY}The Leader{COLOR DARK_GRAY} is here?"))
        case 6:
            msgbox(format("The Tournament is going well so far, but I have to stay ever vigilant, you never know who your next opponent will be."))
        case 7:
            msgbox(format("I have made it to the semi-finals! I'm not gonna lie, I never though I would be able to make it that far."))
        case 8:
            msgbox(format("Damn, Fiona was quite on a different level than my previous fights. It was like she didn't even break a sweat fighitng me!"))
        case 9:
            if(flag(FLAG_BATTLE_WON))
            {
                msgbox(format("I guess that people were correct, you're very strong! Even though Fiona wooped my ass, I would love to fight you as well some day!"))
            }
            else
            {
                msgbox(format("I heard from the other people here you're also quite strong, I'm excited to see who of you two is going to win!"))      
            }
        default:
            msgbox(format("You would be surprised to see how many people out there are actually part of team MOD.\p
            I wonder how many people are secretly team A.S. members?"))
    }
    releaseall
}

script Hall1_EventScript_Beauty{
    lockall
    faceplayer
    switch(var(VAR_STORY_PROGRESSION))
    {
        case 0:
        case 1:
        case 2:
        case 3:
        case 4:
            msgbox(format("I'm sorry dear, but I'm not here to make friends, so just move along."))
        case 5:
            msgbox(format("It's no shame to drop out now dear, we all know that the best Initaitive is sitting right here."))
        case 6:
        case 7:
            msgbox(format("O? you're still in the tournament? Don't worry dear, it's only a matter of time before I'll let you know what defeat is."))
        case 8:
            if(flag(FLAG_BATTLE_WON))
            {
                msgbox(format("Hmp, I don't talk to cheaters!"))
            }
            else
            {
                msgbox(format("Ah, I heard you're my next loser. Hope you had fun in this tournament, but there is no way you can win against such a skillfull person like me."))
            }
        case 9:
            if(flag(FLAG_BATTLE_WON))
            {
                msgbox(format("...\p
                ...\p
                ...\p
                well fought i guess..."))
            }
            else
            {
                msgbox(format("Frankly, I think neither of you two deserve to be in the finals. I'm clearly the best trainer. But there is little I can do to chance the system now."))      
            }
        default:
            msgbox(format("Be careful not to fall off your high horse dear. It's only a matter of time before somewhere far more handsome and skillful will take your place."))
    }
    releaseall
}

script Hall1_Trigger_NotYet{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, Hall1_Movement_MoveBack)
    msgbox(format("You don't have any reason yet to go this way."))
    release
}

movement Hall1_Movement_MoveBack{
    walk_up
}
