const FIONA_LOCAL_ID = 1

mapscripts MODHeadquarters_FionasRoom_MapScripts {

}

script FionasRoom_SignScript_Drawer{
    lockall
    msgbox(format("Fiona seems to have used to bottem two drawers for her clothes and used the top one for some of her personal belongings. The top drawer also contains a very old, blood stained bandage for some reason."))
    if(!flag(FLAG_FIONA_ROOM))
    {
        applymovement(FIONA_LOCAL_ID, CommonMovement_NoticeUp)
        msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: Hé, please don't touch my stuff {PLAYER}!\l 
        I...\l 
        I just had that organised!"))
        applymovement(FIONA_LOCAL_ID, CommonMovement_FaceOriginalDirection)
    }
    releaseall
}

script FionasRoom_SignScript_Book{
    lockall
    msgbox(format("It seems like Fiona left her diary open on the table.\l Do you want to read it?"), MSGBOX_YESNO)
    if(var(VAR_RESULT))
    {
        if(!flag(FLAG_FIONA_ROOM))
        {
            applymovement(FIONA_LOCAL_ID, CommonMovement_FacePlayer)
            msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}:Uhm...\p 
            I hope you understand {PLAYER}, but this stuff is a little bit personal...\p
            So I would appreciate it if you didn't read it."))
            applymovement(FIONA_LOCAL_ID, CommonMovement_FaceOriginalDirection)
        }
        else
        {
            setvar(VAR_TEMP_A, 1)
            message(format("Which diary entry would you like to read?"))
            while(var(VAR_TEMP_A) == 1)
            {
                if(var(VAR_STORY_PROGRESSION) < 10)
                {
                    multichoice(25, 2, MULTI_DIARY_1, 0)
                }
                else
                {
                    multichoice(25, 2, MULTI_DIARY_2, 0)
                }
                closemessage
                switch(var(VAR_RESULT))
                {
                    case 0:
                        msgbox(format("'Dear Diary,\n 
                        today is the day!\l 
                        The day I'm finally going to do something usefull with my life!\l 
                        My mom and brother were not happy with my choice, but I could not just sit around and do nothing anymore!\l
                        My mom seems to eventually understood it, but I fear my brother still hates me for the choice I made.\p
                        I just hope I can show him one day why I did this and why it was neseccary.\l
                        Please Brother, forgive me.'"))
                        message(format("Which diary entry would you like to read?"))
                    case 1:
                        msgbox(format("'Dear Diary,\n
                        I'm so annoyed right now!\l
                        I have been studying really hard and pushing myself to the limit and still I only scored second highest of my class. \p
                        I don't know how they do it, that {PLAYER}. They just seems to be a natural at this!\p
                        I want to hate them, but I just can't help but respect their skill.\l
                        Maybe I should just try to befriend them instead?\l 
                        Maybe they could help me out if I would ever be in need...'"))
                        message(format("Which diary entry would you like to read?"))
                    case 2:
                        msgbox(format("'Dear Diary,\n
                        It's finally time, I have made it through the pre-selection and I'm now applicable for getting my first pokemon.\p
                        I never have had a pokemon before, I wonder which one I will get and how they will be?\p
                        I just hope everything I'm doing is gonna be worth it...\p
                        Brother, if you would ever read this, know that I have done this in your best interest.\l
                        Please forgive our mom for letting me do this and please forgive me for wanting to do it.'"))
                        message(format("Which diary entry would you like to read?"))
                    case 3:
                        if(var(VAR_STORY_PROGRESSION) < 10)
                        {
                            //do nothing
                        }
                        elif(flag(FLAG_TOURNAMENT_LOST))
                        {
                            msgbox(format("'Dear Diary,\n
                            I cannot believe it!\l
                            I won the Tournament! I was sure {PLAYER} would also beat me with this, but no, it was me who won!\l
                            Even {COLOR LIGHT_GRAY}The Leader{COLOR DARK_GRAY} came to me personally and gave me an Shiny Eevee!\l
                            Maybe... Maybe I can still do this...\l
                            Maybe I can still be the sister that protects you Brother..."))
                            message(format("Which diary entry would you like to read?"))
                        }
                        else
                        {
                            msgbox(format("'Dear Diary,\n
                            The outcome I feared has happened.\l 
                            {PLAYER} won the tournament.\p
                            It's not like I don't think they deserve it, I just don't know if I can trust them.\l
                            I would like them to be my friend, but if they become a problem than I fear I won't be strong enough to stop them.\l
                            Which means I won't be strong enough to protect you... Brother.\p
                            I failed you... I'm sorry."))
                            message(format("Which diary entry would you like to read?"))
                        }
                    default:
                        setvar(VAR_TEMP_A, 0)
                        msgbox(format("Enough snooping around for now."))
                }
            } 
        }
    }
    else
    {
        msgbox(format("Better not go through people's stuff without first asking permission."))
    }
    releaseall
}

script FionasRoom_EventScript_Fiona{
    lockall
    faceplayer
    switch(var(VAR_STORY_PROGRESSION))
    {
        case 6:
            msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: Hé {PLAYER}, ready for your first day of the tournament?"))
        case 7:
            msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: Hope the tournament is going great for you! So far, I'm keeping up my end of our agreement."))
        case 8:
            msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: I made it to the finals {PLAYER}! Don't dissapoint me and make sure we meet each other there!"))
        case 9:
            if(flag(FLAG_BATTLE_WON))
            {
                msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: Hé Champion, don't think I'm just going to sulk around.\p
                I'm going to train harder than ever before and the next time you won't defeat me that easily!"))
            }
            else
            {
                msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: Today is the day {PLAYER}.\n Today we're going to do that pokemon battle we promised each other. Once you're ready, go to the Battle Dojo.\l
                I'll be ready!"))
            }
        default:
            msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: Oh, hé {PLAYER}! Welcome to my room!\l Feel free to look around the place!\p
            If you need anything let me know!\p
            I do intend on getting an early night though, so hope you don't mind me going to bed soon.\p
            Maybe you should do the same, but that is entirely up to you!"))
    }
    applymovement(FIONA_LOCAL_ID, CommonMovement_FaceOriginalDirection)
    releaseall
}

script FionasRoom_SignScript_Computer{
    lockall
    msgbox(format("It's Fiona's computer. You can see a background of a cute cat hanging on a tree with the text: 'Hang in there!'.\p Not much else you can do on the computer since you don't know the password."))
    if(!flag(FLAG_FIONA_ROOM))
    {
        applymovement(FIONA_LOCAL_ID, CommonMovement_NoticeUp)
        msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: What are you trying to do?\p
        Anyways, don't think that I'm just going to tell you my password and good luck guessing it!\p
        I know how to keep nosey people out of my affairs, if you know what I mean."))
        applymovement(FIONA_LOCAL_ID, CommonMovement_FaceOriginalDirection)
    }
    releaseall
}

script FionasRoom_SignScript_Bed{
    lockall
    msgbox(format("It's Fiona's bed!\p
    Do you want to sleep in it?"), MSGBOX_YESNO)
    if(var(VAR_RESULT))
    {
        if(var(VAR_STORY_PROGRESSION) < 5)
        {
            msgbox("Now is not the time to go to bed...")
        }
        elif(!flag(FLAG_FIONA_ROOM))
        {
            msgbox(format("{COLOR RED}Fiona{COLOR DARK_GRAY}: uhm... please don't take this the wrong way, but I just see you as a friend {PLAYER}.\p
            I...\n I don't feel that comfortable sharing a bed with you..."))
        }
        else
        {
            setrespawn(HEAL_LOCATION_MOD_HEADQUARTERS_FIONAROOM)
            fadescreen(1)
            special(HealPlayerParty)
            playfanfare(MUS_HEAL)
            waitfanfare
            fadescreen(0)
        }
    }
    else
    {
        msgbox(format("Why would you want to sleep in Fiona's bed?"))
    }
    releaseall
}

script FionasRoom_SignScript_Table{
    msgbox(format("Why does Fiona also have an unique table and carpet?"))
    releaseall
}