const DENEITH_LOCALID = 1
const AIRANEA_LOCALID = 2

mapscripts CliffToNowhere_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: CliffToNowhere_ShowDeneith
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_STORY_PROGRESSION, 13: CliffToNowhere_EscapeSequence
    ]
}

script CliffToNowhere_ShowDeneith{
    if(var(VAR_STORY_PROGRESSION) == 13)
    {
        setflag(FLAG_FREE_SIMULATION)
        clearflag(FLAG_HIDE_DENEITH)
        clearflag(FLAG_HIDE_AIRANEA)
    }
    else
    {
        setflag(FLAG_HIDE_DENEITH)
        setflag(FLAG_HIDE_AIRANEA)
    }
}

script CliffToNowhere_EscapeSequence{
    lockall
    hideobjectat(DENEITH_LOCALID, MAP_CLIFF_TO_NOWHERE)
    hideobjectat(AIRANEA_LOCALID, MAP_CLIFF_TO_NOWHERE)
    setobjectxy(DENEITH_LOCALID, 16, 15)
    setobjectxy(AIRANEA_LOCALID, 16, 15)

    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_WalkDown1)
    waitmovement(0)

    msgbox(format("???: STOP HER, RIGHT NOW!"), MSGBOX_DEFAULT)
    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_NoticeUp)
    waitmovement(0)
    opendoor(16, 15)
    waitdooranim
    showobjectat(DENEITH_LOCALID, MAP_CLIFF_TO_NOWHERE)
    applymovement(DENEITH_LOCALID, CliffToNowhere_RunDown)
    waitmovement(0)
    closedoor(16, 15)
    waitdooranim
    applymovement(DENEITH_LOCALID, CliffToNowhere_BreakDoor)
    waitmovement(0)
    playse(SE_BREAKABLE_DOOR)
    waitse
    msgbox(format("{COLOR LIGHT_RED}???{COLOR DARK_GRAY}: What are you doing? \n
    Open this door right now!\l
    We can't let {COLOR RED}her{COLOR DARK_GRAY} escape!"))
    msgbox(format("???: W-w-we're trying {COLOR LIGHT_RED}Airanea{COLOR DARK_GRAY}, but it seems {COLOR RED}she{COLOR DARK_GRAY} has broken the door..."))
    msgbox(format("{COLOR LIGHT_RED}Airanea{COLOR DARK_GRAY}: Then go fix it!"))
    msgbox(format("{COLOR RED}???{COLOR DARK_GRAY}: that should keep them busy at least..."))
    applymovement(DENEITH_LOCALID, CommonMovement_NoticeDown)
    waitmovement(0)
    msgbox(format("{COLOR RED}???{COLOR DARK_GRAY}: Damn, seems like I didn't manage to outsmart all {COLOR LIGHT_GRAY}his{COLOR DARK_GRAY} henchman.\p
    Wait... I know you!\n
    You're that Special Initiative that was talking to {COLOR LIGHT_GRAY}The Leader{COLOR DARK_GRAY}!\p
    I would love to stick around and talk some more with you, but I've places to be.\p
    ...\p
    What do you mean you're not going to let me go?\p
    Sigh... I'm not really prepared for a fight like this, but I've gotten too far to let someone stop me now!\l
    Bring it on Hotshot!"))
    setvar(VAR_STORY_PROGRESSION, 14)
    special(HealPlayerParty)
    trainerbattle_no_intro(TRAINER_DENEITH_1, CliffToNowhere_DeneithLost)
    setvar(VAR_STORY_PROGRESSION, 15)
    msgbox(format("{COLOR RED}Deneith{COLOR DARK_GRAY}: And I would have gotten away with it if it wasn't for you...\p
    No matter, hear take your stupid usb back, we don't need it anyways.\l
    I was just a distraction so that nobody would notice Zero hacking their PCs and get all the data we needed.\p
    Still, I would recommend you watch what is on that stick.\l
    It'll contain some secrets of team MOD they will never disclose to you personally.\p
    Until our next encounter Hotshot!"))
    closemessage
    setflag(FLAG_USB_STICK)
    applymovement(OBJ_EVENT_ID_PLAYER, CliffToNowhere_PushedBack)
    applymovement(DENEITH_LOCALID, CliffToNowhere_Jumpoff)
    waitmovement(0)
    setfieldeffectargument(0, DENEITH_LOCALID)
    setfieldeffectargument(1, 2)
    setfieldeffectargument(2, 6)
    dofieldeffect(FLDEFF_NPCFLY_OUT_CUSTOM)
    playse(SE_M_FLY)
    waitfieldeffect(FLDEFF_NPCFLY_OUT_CUSTOM)
    removeobject(DENEITH_LOCALID, MAP_CLIFF_TO_NOWHERE)
    setflag(FLAG_HIDE_DENEITH)
    opendoor(16, 15)
    waitdooranim
    showobjectat(AIRANEA_LOCALID, MAP_CLIFF_TO_NOWHERE)
    applymovement(AIRANEA_LOCALID, CliffToNowhere_Pursue)
    waitmovement(0)
    closedoor(16, 15)
    waitdooranim
    msgbox(format("{COLOR LIGHT_RED}Airanea{COLOR DARK_GRAY}: SHIT!\n Just lost them."))
    applymovement(AIRANEA_LOCALID, CliffToNowhere_BackToPlayer)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_LookRight)
    waitmovement(0)
    msgbox(format("{COLOR LIGHT_RED}Airanea{COLOR DARK_GRAY}: I thank you for your services {PLAYER}. You managed to distract her almost long enough for us to catch her before she escaped.\p
    I know you tried your best, but sadly these team A.S. members don't care for the result in a pokemon battle.\p
    This is important though, did you by any chance manage to snatch something of her or did she drop something by accident?\l
    I mean anything, did you manage to get anything at all from her?"), MSGBOX_YESNO)
    if(var(VAR_RESULT))
    {
        msgbox(format("{COLOR LIGHT_RED}Airanea{COLOR DARK_GRAY}: Great job apprenhending this {PLAYER}! \l   
        This usb contains some...\l
        'sensative' data we would prefer to not fall in the enemies hands.\p
        We will take it over from here {PLAYER}.\p
        Please take this, as thanks for your hard work and loyalty."))
        giveitem(ITEM_BIG_NUGGET, 1)
        clearflag(FLAG_USB_STICK)
    }
    else
    {
        msgbox(format("{COLOR LIGHT_RED}Airanea{COLOR DARK_GRAY}: DAMMIT!\p
        ...\p
        You don't need to worry too much about it {PLAYER}, let me take care of it from here, you go on your quest now, we will probably speak each other another time."))
    }
    msgbox(format("{COLOR LIGHT_RED}Airanea{COLOR DARK_GRAY}: One more thing though, be carefull around these A.S. Members \p
    Though you'll certainly be handsomely rewarded if you can bring any of them in, it has a 99.72% change of ending in your death. \p
    So please be very carefull around them, \n
    it would be shame to lose such a great asset."), MSGBOX_DEFAULT)
    closemessage
    applymovement(AIRANEA_LOCALID, CliffToNowhere_BackToEntrance)
    waitmovement(0)
    opendoor(16, 15)
    waitdooranim
    applymovement(AIRANEA_LOCALID, CommonMovement_WalkUp1)
    waitmovement(0)
    hideobjectat(AIRANEA_LOCALID, MAP_CLIFF_TO_NOWHERE)
    closedoor(16, 15)
    waitdooranim
    setflag(FLAG_HIDE_AIRANEA)
    releaseall
}

script CliffToNowhere_Event_DemoEnding
{
    lockall
    faceplayer
    msgbox(format("Congratulations! \p
    This is the ending of the demo, the rest of the game will be worked on in the next couple of years. \p
    feel free to share feedback about the demo and what things you would like to see improved in the full version. \l
    Feel also free to share any spelling mistakes made in the demo, there are probably a lot. \p
    Found any exploits or mistakes? any glitches or bugs? Anything that didn't work quite as well? \l
    All feedback is welcome! \p
    You're free to explore the MOD headquarters for as long as you like, but the official demo ends here. \p
    Also a warning, I cannot guarantee that any progress you made in this demo will be kept in the full release. \p
    With that being said:\p 
    Thank you for playing this demo!"))
    releaseall
}

text CliffToNowhere_DeneithLost {
    format("Damn, I guess I should have been beter prepared for this")
}

movement CliffToNowhere_RunDown {
    walk_fast_down
    face_up
}

movement CliffToNowhere_BreakDoor {
    walk_in_place_fast_up
    walk_in_place_fast_up
    walk_in_place_fast_up
    walk_fast_up
    lock_facing_direction
    jump_down
    unlock_facing_direction
}

movement CliffToNowhere_PushedBack{
    lock_facing_direction
    walk_fast_down
    unlock_facing_direction
    face_right
    lock_facing_direction
    walk_fast_left
    unlock_facing_direction
    face_down
}

movement CliffToNowhere_Jumpoff{
    walk_fast_down * 5
    jump_2_down
}

movement CliffToNowhere_Pursue{
    walk_fast_down * 7
}

movement CliffToNowhere_BackToPlayer{
    walk_up * 4
    face_left
}

movement CliffToNowhere_BackToEntrance {
    walk_up * 2    
}