const FIONA_ID = 4
const NINJABOY_LOCALID = 5
const STATUEGUY_LOCALID = 8 
const LADY_LOCALID = 1
const GUY_LOCALID = 3
const LEADER_LOCALID = 2

mapscripts MODHeadquarters_DormHall1_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Dormhall1_OnTransition
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_STORY_PROGRESSION, 1: Dormhall1_Frame
        VAR_STORY_PROGRESSION, 10: Dormhall1_Frame_TournamentOver
    ]
}

script Dormhall1_OnTransition{
    setvar(VAR_ALWAYS_ON, 1)
    setvar(VAR_HIDE_OBJECTS, 1)
    if(var(VAR_STORY_PROGRESSION) == 0)
    {
        setvar(VAR_STORY_PROGRESSION, 1)
        setflag(FLAG_SYS_B_DASH)
        setflag(FLAG_SYS_ALWAYS_ON)
        setflag(FLAG_REVERSE_ROLLCALL_FLAG)
        setflag(FLAG_FIONA_REVERSE_ROLLCALL_FLAG)
        setflag(FLAG_FIONA_ROOM)
        setflag(FLAG_REVERSED_GUARD)
        setflag(FLAG_HIDE_LEADER)
        setflag(FLAG_HIDE_DENEITH)
        setflag(FLAG_HIDE_REPLICA_SIMULATION_SISTER)
    }
    if(flag(FLAG_TOURNAMENT_LOST))
    {
        setvar(VAR_STORY_PROGRESSION, 10)
        clearflag(FLAG_FIRST_RIVAL_MEETING)
        clearflag(FLAG_HIDE_LEADER)
    }
}

script Dormhall1_Frame_TournamentOver{
    lockall
    if(flag(FLAG_TOURNAMENT_LOST))
    {
    }
    else
    {
        call(DormHall1_WonTournament)
    }
    releaseall
}

script Dormhall1_Frame{
    lock
    applymovement(FIONA_ID, Dormhall1_Movement_NoticePlayer)
    waitmovement(0)
    msgbox(format("{COLOR RED}???{COLOR DARK_GRAY}: {COLOR BLUE}{PLAYER}! {COLOR DARK_GRAY}You're awake. I have been waiting for you!"))

    applymovement(FIONA_ID, Dormhall1_Movement_MoveToPlayer)
    waitmovement(0)
    msgbox(format("{COLOR RED}???:{COLOR DARK_GRAY} What is that look on your face?\n Don't you remember me?\p
    {COLOR RED}Fiona{COLOR DARK_GRAY}: It's me, Fiona! I was the second best of the class, just below you! \p
    I guess being the second best at anything is not that impressive huh? \p
    Anyway, today is the day, we're gonna recieve our first pokemon! I'm so excited, aren't you?\p
    I'll go ahead.\n Remember, we get our pokemon from the professor inside the headquarters.\l he is down the hallway, directly below our Dormhall.\p
    Catch you later, {COLOR BLUE}{PLAYER}{COLOR DARK_GRAY}!"))

    applymovement(FIONA_ID, Dormhall1_Movement_WalkAway)
    waitmovement(0)
    setflag(FLAG_FIRST_RIVAL_MEETING)
    setvar(VAR_STORY_PROGRESSION, 2)
    removeobject(FIONA_ID, MAP_MODHEADQUARTERS_DORM_HALL1)
    release
}

script DormHall1_EventScript_LadyInitiative{
    lockall
    switch(var(VAR_STORY_PROGRESSION))
    {
        case 0:
        case 1:
        case 2:
        case 3:
        case 4:
            msgbox(format("Does he really think we don't notice him?"))
        case 5:
            msgbox(format("I never had a pokemon before and now I have to fight in a tournament. I wonder how it will go..."))
        case 6:
            msgbox(format("Did you really have to go that hard on me? You could have at least given me a chance!"))
        case 7:
            msgbox(format("I saw your fight! Fiona is really a powerfull Pokemon Trainer\p She has a good chance at winning this whole Tournament!"))
        case 8:
            msgbox(format("You can always ask for a rematch!\n I'll make sure I won't lose this time!"))
        case 9:
            if(flag(FLAG_BATTLE_WON))
            {
                msgbox(format("I really thought Fiona had the upper hand in that battle for a moment!"))
            }
            else
            {
                msgbox(format("Todays final is gonna be very interesting! Fiona and {PLAYER} have both shown incredible skill!"))      
            }
        default:
            msgbox(format("{COLOR BLUE}Vera{COLOR DARK_GRAY}: The world outside can be a scary place, but at least with my Pokemon I can make sure I'm safe."))
    }
    releaseall
}

script DormHall1_EventScript_ManInitiative{
    lockall
    switch(var(VAR_STORY_PROGRESSION))
    {
        case 0:
        case 1:
        case 2:
        case 3:
        case 4:
            msgbox(format("Just ignore that guy, let him enjoy the moment."))
        case 5:
            msgbox(format("Don't worry about it, I'm sure {COLOR LIGHT_GRAY}The Leader{COLOR DARK_GRAY} made this a safe environment to test out the waters."))
        case 6:
            msgbox(format("I'm sorry. I guess I was just a bit too much focussed on impressing Surge and possibly the {COLOR LIGHT_GRAY}The Leader{COLOR DARK_GRAY}"))
        case 7:
            msgbox(format("Wow, that Fiona Lass is really something else, she wiped the floor with me."))
        case 8:
            msgbox(format("Seeing the other Initatives fight each other makes me realize I still have much to learn."))
        case 9:
            if(flag(FLAG_BATTLE_WON))
            {
                msgbox(format("I was so close to be the tournament champion losers, guess I have to live with the fact I'm just a regular loser.\p
                ...\p
                Don't you dare, Vera!"))
            }
            else
            {
                msgbox(format("It certainly is! I hope Fiona will win, at least then I can say I lost to the Tournament Champion..."))      
            }
        default:
            msgbox(format("{COLOR BLUE}Tristan{COLOR DARK_GRAY}: You should watch out for those A.S. members, they look very strong..."))
    }
    releaseall
}

script DormHall1_EventScript_StatueInitiative{
    lockall
    faceplayer
    switch(var(VAR_STORY_PROGRESSION))
    {
        case 0:
        case 1:
        case 2:
        case 3:
        case 4:
            msgbox(format("I wonder what kind of pokemon these statues are supposed to be."))
        case 5:
            msgbox(format("With the {COLOR LIGHT_GRAY}The Leader{COLOR DARK_GRAY} here, who knows what kinds of things we can learn from {COLOR LIGHT_GRAY}Him{COLOR DARK_GRAY}."))
        case 6:
            msgbox(format("I underestimated how little I actually know about using my skills out on the field."))
        case 7:
        case 8:
            msgbox(format("I have been reflecting on my failures and I think I should try becoming more of do-er and less of learner."))
        case 9:
            if(flag(FLAG_BATTLE_WON))
            {
                msgbox(format("Impressive {PLAYER}! I do hope you would allow a low level trainer like me take you on sometime!"))
            }
            else
            {
                msgbox(format("You know, watching you have made me realize I have still a long way to go, but that does not mean I intend to give up that easily!"))      
            }
        default:
            msgbox(format("{COLOR BLUE}Victor{COLOR DARK_GRAY}: Strange how no matter what kind of pokemon I encounter, I have never seen a pokemon like this statue..."))
    }
    
    releaseall
}

script Dormhall1_EventScript_NinjaInitiative{
    lockall
    faceplayer
    switch(var(VAR_STORY_PROGRESSION))
    {
        case 0:
        case 1:
        case 2:
        case 3:
        case 4:
            msgbox(format("sssh, don't let them know I'm hiding here."))
        case 5:
            msgbox(format("I have to improve my ninja skills if I want to impress the {COLOR LIGHT_GRAY}The Leader{COLOR DARK_GRAY}"))
        case 6:
            msgbox(format("I thought I was very sneaky with my tactics, but my opponent saw straight through me.\p 
            I have to improve my sneaking skills if I ever want to make it far in MOD!"))
        case 7:
        case 8:
            msgbox(format("I'm impressed you have gotten this far without making sure you got the upper hand on your opponent."))
        case 9:
            if(flag(FLAG_BATTLE_WON))
            {
                msgbox(format("Congratulations {PLAYER}, you have done a great job! If you ever feel like someone is watching you, just... ignore it for me will you?"))
            }
            else
            {
                msgbox(format("You and Fiona are both fascinating trainers to watch!\p
                I might try to shadow you for a while to learn a thing or two from you!"))     
            } 
        default:
            msgbox(format("{COLOR BLUE}Niko{COLOR DARK_GRAY}: Fighting head on is not always the best tactic, I have learned that sneaking about can give you a better idea what is going on than to beat the answers out of people."))
    }
    applymovement(NINJABOY_LOCALID, Dormhall1_Movement_LookOriginalDireciton)
    releaseall
}

script Dormhall1_EventScript_GuardTop{
    lockall
    faceplayer
    msgbox(format("This area lead to a special area not meant for initiatives, you better move to other way."))
    releaseall
}

script Dormhall1_EventScript_GuardBottem{
    lockall
    faceplayer
    msgbox(format("Our duty is to prevent any nosey initiative from entering this area."))
    releaseall
}

script Dormhall1_SignScript_StatueNormal{
    lockall
    msgbox(format("It seems to be of a statue of a pokemon, though the erosion make it hard to determine which pokemon it is supposed to be."))
    releaseall
}

script Dormhall1_SignScript_SpecialStatueBack{
    lockall
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    switch(var(VAR_RESULT))
    {
        case 1:
        case 3:
        case 4:
            msgbox(format("HÃ©, there seems to be something etched on the back of this statue:\p
            'forgotten, but not gone.\n
            -Team A.S.'"))
        case 2:
            msgbox(format("It seems to be of a statue of a pokemon, though the erosion make it hard to determine which pokemon it is supposed to be."))
    }
    releaseall
}

script Dormhall1_Trigger_NoEntry{
    lockall    
    applymovement(OBJ_EVENT_ID_PLAYER, Dormhall1_Movement_MoveBack)
    msgbox(format("Sorry kid, no entry without prior authorization."))
    waitmovement(0)
    releaseall
}

script Dormhall1_Trigger_SetWarpValue1{
    setvar(VAR_DYNAMIC_DORMWARP, 1)
    release
}

script Dormhall1_Trigger_SetWarpValue2{
    setvar(VAR_DYNAMIC_DORMWARP, 2)
    release
}

script Dormhall1_Trigger_SetWarpValue3{
    setvar(VAR_DYNAMIC_DORMWARP, 3)
    release
}

script DormHall1_WonTournament{
    msgbox(format("{COLOR LIGHT_GRAY}???{COLOR DARK_GRAY}: {PLAYER}, stay there for a moment"))
    applymovement(NINJABOY_LOCALID, Dormhall1_Movement_ShockLeft)
    applymovement(GUY_LOCALID, Dormhall1_Movement_ShockLeft)
    applymovement(LADY_LOCALID, Dormhall1_Movement_NoticePlayer)
    applymovement(STATUEGUY_LOCALID, Dormhall1_Movement_NoticePlayer)
    closemessage
    applymovement(LEADER_LOCALID, Dormhall1_Movement_Move_To_PlayerWon)
    waitmovement(0)
    msgbox(format("{COLOR LIGHT_GRAY}Leader{COLOR DARK_GRAY}: I would like to talk to you personally, if you wouldn't mind.\p
    But let's do that somewhere with less prying eyes and gawking mounths.\l
    If you would please follow me."))
    warp(MAP_MODHEADQUARTERS_ENTRANCE_HALL, 2, 2)
}


movement Dormhall1_Movement_NoticePlayer{
    emote_exclamation_mark
}

movement Dormhall1_Movement_ShockLeft{
    emote_exclamation_mark
    face_right
}

movement Dormhall1_Movement_MoveToPlayer{
    walk_up * 2
}

movement Dormhall1_Movement_WalkAway{
    walk_down * 4
    walk_left * 4
    walk_down
}

movement Dormhall1_Movement_MoveBack{
    walk_left
}

movement Dormhall1_Movement_LookOriginalDireciton{
    face_down
}

movement Dormhall1_Movement_Move_To_PlayerWon{
    walk_slow_right*8
    walk_slow_up*2
}